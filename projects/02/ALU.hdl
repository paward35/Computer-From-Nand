// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    Mux16(a=x, b=false, sel=zx, out=xAfterZx);
    Not16(in=xAfterZx, out=NotxAfterZx);
    Mux16(a=xAfterZx, b=NotxAfterZx, sel=nx, out=xAfterZxNx);

    Mux16(a=y, b=false, sel=zy, out=yAfterZy);
    Not16(in=yAfterZy, out=NotyAfterZy);
    Mux16(a=yAfterZy, b=NotyAfterZy, sel=ny, out=yAfterZyNy);

    Add16(a=xAfterZxNx, b=yAfterZyNy, out=XplusY);
    And16(a=xAfterZxNx, b=yAfterZyNy, out=XndY);
    Mux16(a=XndY, b=XplusY, sel=f, out=outF);
    
    Not16(in=outF, out=NotOutF);
    Mux16(a=outF, b=NotOutF, sel=no, out=out, out[15]=msb, out[0..7]=lsh, out[8..15]=msh);

    And(a=msb, b=true, out=ng);
    
    Or8Way(in=lsh, out=h1);
    Or8Way(in=msh, out=h2);
    Or(a=h1, b=h2, out=notZero);
    Not(in=notZero, out=zr);
}