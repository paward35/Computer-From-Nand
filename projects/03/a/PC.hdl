// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with increment, load, and reset modes.
 * if      (inc(t))   out(t+1) = out(t) + 1
 * else if (load(t))  out(t+1) = in(t)
 * else if (reset(t)) out(t+1) = 0
 * else               out(t+1) = out(t)
 *
 * To select a mode, assert the relevant control bit,
 * and de-assert the other two bits. 
 */
CHIP PC {
    IN in[16], inc, load, reset;
    OUT out[16];
    
    PARTS:
    Inc16(in=count, out=incCount);
    
    Mux16(a=count, b=incCount, sel=inc, out=incOrNothing);
    Mux16(a=incOrNothing, b=in, sel=load, out=loadOrInc);
    Mux16(a=loadOrInc, b=false, sel=reset, out=newCount);
    
    Register(in=newCount, load=true, out=count, out=out);

}
